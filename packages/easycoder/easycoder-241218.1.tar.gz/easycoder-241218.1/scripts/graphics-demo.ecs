!   Graphics demo

    script Graphics-Demo

    rectangle BlueRect
    ellipse RedEllipse
    ellipse GreenCircle
    text WhiteText
    file File
    variable Spec
    variable UpDown
    variable Left
    variable Top
    variable N

!    debug step

    create screen at 300 300 size 640 480 fill color yellow

    open File `json/graphics-demo.json` for reading
    read Spec from File
    close File

    render Spec

    attach BlueRect to `bluerect`
    on click BlueRect print `Click Blue`
    attach RedEllipse to `redellipse`
    on click RedEllipse print `Click Red`
    attach GreenCircle to `greencircle`
    on click GreenCircle print `Click Green`

    put attribute `left` of RedEllipse into Left
    put attribute `top` of RedEllipse into Top

    clear UpDown
    put 0 into N
    on tick
    begin
        if UpDown
        begin
            if N is less than 10
            begin
                move BlueRect by -10 -10
            end
            else
            begin
                put 0 into N
                clear UpDown
            end
            move RedEllipse to Left Top
            add 5 to Left
        end
        else
        begin
            if N is less than 10
            begin
                move BlueRect by 10 10
            end
            else
            begin
                put 0 into N
                set UpDown
            end
            move RedEllipse to Left Top
            take 5 from Left
        end
        increment N
    end
    show screen
    stop
